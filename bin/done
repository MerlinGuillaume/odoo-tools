#!/usr/bin/fish

argparse 'amend' -- $argv

function modules --inherit _flag_amend
  if set -q _flag_amend
    set files (git diff-tree --no-commit-id --name-only -r @)
  end
  set -a files (git status --porcelain | rg '^[AMRDC]+\s+(?P<path>.*)' -or'$1')
  for file in $files
    set parts (string split / $file)
    switch (workspace -r)
      case 'odoo'
        if test "$parts[1]" = "addons" && set -q parts[2]
          set -a modules $parts[2]
        end
      case 'enterprise'
        if set -q parts[1]
          set -a modules $parts[1]
        end
      case '*'
        exit 1
    end
  end

  if set -q modules
    string join ', ' $modules
  else
    echo
  end
end

function parse_ticket_id
  string match -rq 'opw-(?P<id>\d+)' $argv[1] && echo $id || echo TODO
end

function opw
  parse_ticket_id (git rev-parse --abbrev-ref HEAD)
end

function msg
  string join \n\n '[FIX] '(modules)':' 'Step to follow' 'Cause of the issue' 'Solution' 'opw-'(opw)
end

if set -q _flag_amend
  git commit --amend -eF (msg | psub)
else
  git commit -eF (msg | psub)
end
