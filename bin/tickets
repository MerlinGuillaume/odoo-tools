#!/usr/bin/fish

set base_url 'https://www.odoo.com'
set user_id '2036938'
set database 'openerp'
set project_id '49'
set password "$ODOO_PASS"

if test -z "$password"
  echo ODOO_PASS not set
  exit 1
end

function rpc_call
  set args -s $database $user_id -s $password $argv
  jo -- -s jsonrpc=2.0 params=(jo service=object method=execute args=(jo -a -- $args))
end

# search_read(self, domain=None, fields=None, offset=0, limit=None, order=None)
function search_read
  argparse model= fields= offset= limit= -- $argv
  if not set -q argv[1]
    echo missing domain
    exit 1
  end
  set -q _flag_offset || set _flag_offset 0
  set -q _flag_limit || set _flag_limit 0
  set fields (set -q _flag_fields && jo -a -- (string split -- ',' $_flag_fields) || echo '[]')
  set params (jo -a $argv) $fields $_flag_offset $_flag_limit
  rpc_call $_flag_model search_read $params
end

function request_body
  set domain (jo -a project_id = $project_id) (jo -a user_id = $user_id) (jo -a stage_id in '[194, 227]')
  search_read --limit 25 --model project.task --fields id,name,stage_id,kanban_state $domain
end

function search_read_tasks
  request_body | curl -q --data-binary @- $base_url/jsonrpc --header 'Content-Type: application/json' 2>/dev/null || exit 1
end

set response (search_read_tasks)

function ticket_url
  set url "$base_url/web#id=$argv[1]&active_id=$project_id&model=project.task&view_type=form&cids=1"
  # TODO check in the terminfo database if the terminal supports this
  echo -e '\e]8;;'"$url"'\a'$argv[2]'\e]8;;\a'
end

function print_section
  set -q argv[2] || return
  set -q first && echo

  set_color -o brcyan
  echo $argv[1]
  set_color normal
  echo

  set -g first

  for ticket in $argv[2..]
    echo $ticket | read -d ',' id state name
    echo -n "  "
    if test "$state" = "done"
      set_color -u green
    else if test "$state" = "blocked"
      set_color -u red
    else
      set_color -u
    end
    ticket_url $id "$name"
    set_color normal
  end
end

set jq_query '.result[] | select(.stage_id[0] == ($STAGE_ID | tonumber)) | [.id,.kanban_state,.name] | join(",")'
set bugfix (echo $response | jq --arg STAGE_ID 194 $jq_query -cr)
print_section 'Bug Fixes' $bugfix
set feedback (echo $response | jq --arg STAGE_ID 227 $jq_query -cr)
print_section 'Cust. Feedback' $feedback
